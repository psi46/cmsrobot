<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="clkCleanupChuck2">
    <Locals>
      <Local name="nIdx" type="num" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  switchMagnet=false  
  statusCleanupChuck2 = colorRunning

  call selectedCheck()
  
  // -- pick vacuum chuck2's and put them back to their original place
  for nIdx=0 to 5
    if !bSelectItem or (bSelectedItems[nIdx]==true)
      // -- move over final hdi position (where the chuck is from gluing) and down
      call moveDown(hdiFinal[nIdx], mVerySlow, trZero)
      //movel(appro(hdiFinal[nIdx], trZsafe), tTool, mNomSpeed)
      //movel(hdiFinal[nIdx], tTool, mVerySlow)
      waitEndMove()
   
      // -- lift chuck2
      switchMagnet=true
      delay(waitShort)
      movel(appro(hdiFinal[nIdx], trZsafe), tTool, mSlow)

      // -- move over/to initial chuck position
      movel(appro(chuck2[nIdx], trZsafe), tTool, mNomSpeed)
      waitEndMove()
      movel(chuck2[nIdx], tTool, mVerySlow)
      waitEndMove()

      // -- release it
      switchMagnet=false

      // -- move up 
      movel(appro(chuck2[nIdx], trZsafe) ,tTool, mSlow)
    endIf
  endFor
 
  // -- move back somewhere
  waitEndMove()
  movej(jInitialPosition, tTool, mNomSpeed)
  waitEndMove()
  call print("cleanup (modules) done")
  statusCleanupChuck2 = colorOn
  statusDoModules = colorGray
  call selectedClear()
end]]></Code>
  </Program>
</Programs>