<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="doModules">
    <Locals>
      <Local name="nIdx" type="num" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  while true
    if bDoModules==true
      call selectedCheck()
      call print("starting doModules")
      statusDoModules = colorRunning
      statusCleanupChuck1 = colorGray

      // -- check that relevant vacuum is active
      if !bSelectItem
        // -- check all
        call print("check vacuum on all")
        delay(waitGlue)
        for nIdx = 0 to 5
          if statusValves[sensorValve[nIdx]] == 0
            bDoModules = false
            statusDoModules = colorError
            call print("Error: no vacuum at position" + toString("1.0", nIdx))
          endIf
        endFor
      else 
        // -- check only those that are selected
        call print("check vacuum on selection")
        delay(waitGlue)
        for nIdx = 0 to 5
          if statusValves[sensorValve[nIdx]] == 0 and bSelectedItems[nIdx] == true
            bDoModules = false
            statusDoModules = colorError
            call print("Error: no vacuum at position" + toString("1.0", nIdx))
          endIf
        endFor
      endIf
      
      if bDoModules == false
        call print("Fix missing vacuum and try again")
      endIf
    endIf
    
    // -- return above does not work. It terminates the task!
    if bDoModules==true
      statusDoModules = colorRunning
      call setSpeed()
      
      if !bSkipGlue[0]
        call print("picking glue stamp")
        // -- pick glue stamp
        call moveDown(moduleGlueStampHome, mSlow, trZero, false)
        waitEndMove()
        switchMagnet = true
        movel(appro(moduleGlueStampHome, trZsafe), tTool, mSlow)

        // -- dip stamp into glue before starting the normal procedure
        call moveDown(glueFirstPoint[1], mVerySlow, trZero, false)
        waitEndMove()
        delay(waitGlue)
        call moveStampUp(glueFirstPoint[0], mSlow)

        // -- apply glue stamp to all positions  
        //    divide approach onto /from glue position into two steps
        //    (1) slowly to trApproach above it
        //    (2) extremely slowly up from glue to trApproach 
        for nIdx=0 to 5
          if !bSelectItem or (bSelectedItems[nIdx]==true)
            call setSpeed()
            if bDoModules==true
              call moveDown(gluePickPoint2[nIdx], mSlow, trZero, false)
              delay(waitGlue)

              call moveStampUp(gluePickPoint2[nIdx], mVerySlow)
              
              // -- move to where the glue should be applied (the final hdi position)
              call print("applying glue to position " + toString("1.0", nIdx))
              call moveDown(hdiFinal[nIdx], mSlow, trStamp, true)
              delay(waitGlue)
              call moveStampUp(hdiFinal[nIdx], mExtremelySlow)
            endIf
          endIf
        endFor
        
        // -- Drop glue stamp
        if bDoModules==true
          call setSpeed()
          call print("put away glue stamp")
          movel(appro(moduleGlueStampEnd, trZsafe), tTool, mFast)
          call moveDown(moduleGlueStampEnd, mVerySlow, trZero, true)
          switchMagnet = false
          movel(appro(moduleGlueStampEnd, trZsafe), tTool, mNomSpeed)
          waitEndMove()
        endIf
      endIf
      
      if !bSelectItem
        call print("handling all modules")
      else 
        call print("handling selected modules")
      endIf
      
      // -- move over chuck2 position
      movel(appro(chuck2[0], trZsafe), tTool, mNomSpeed)
      
      // -- pick weighted vacuum chucks and lift airex frames to HDIs
      for nIdx=0 to 5
        if !bSelectItem or (bSelectedItems[nIdx]==true)
          call setSpeed()
          if bDoModules==true
            // -- move over/to chuck
            call moveDown(chuck2[nIdx], mSlow, trZero, true)
            // -- the following cures the click when the chuck 'jumps' up a bit
            movel(appro(chuck2[nIdx], trPressTwo), tTool, mVerySlow)          
            waitEndMove()

            // -- pick it
            switchMagnet = true
            delay(waitShort)
            call print("picking module " + toString("1.0", nIdx))
            movel(appro(chuck2[nIdx], trZsafe), tTool, mVerySlow)
            waitEndMove()
            
            // -- move to hdi initial location (where the airex frame had been glued) and lower
            call moveDown(airexFinal[nIdx], mVerySlow, trZero, true)
        
            // -- release hdi vacuum to allow picking up
            call toggleValve(hdiValve[nIdx])
            delay(waitShort)
            switchVac0=true
            switchVac1=true
            delay(waitShort)

            // -- move up
            call print("placing hdi " + toString("1.0", nIdx))
            movel(appro(airexFinal[nIdx], trZclose), tTool, mSlow)
            // likely not required waitEndMove()
            movel(appro(airexFinal[nIdx], trZsafe), tTool, mNomSpeed)
            // likely not required waitEndMove()
            
            // -- move to final location
            call moveDown(hdiFinal[nIdx], mExtremelySlow, trZero, true)
            switchVac0=false
            switchVac1=false
            switchMagnet=false

            delay(waitShort)

            // -- move up again
            movel(appro(hdiFinal[nIdx], trZclose), tTool, mSlow)
            movel(appro(hdiFinal[nIdx], trZsafe), tTool, mNomSpeed)
            waitEndMove()
          endIf

        endIf
      endFor

      // -- move back somewhere
      call print("parking robot")
      waitEndMove()
      movej(jInitialPosition, tTool, mNomSpeed)
      waitEndMove()
      statusDoModules = colorOn
      
      call selectedClear()
      bDoModules = false
      call print("glue modules done")
    endIf

    delay(waitShort)
  endWhile

end]]></Code>
  </Program>
</Programs>