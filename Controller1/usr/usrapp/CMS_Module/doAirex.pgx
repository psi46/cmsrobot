<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="doAirex">
    <Locals>
      <Local name="nAirexIdx" type="num" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  while true

    if bDoAirex==true
      call selectedCheck()
      call print("starting doAirex")
      
      // -- check that relevant vacuum is active
      if !bSelectItem
        // -- check all
        call print("check vacuum on all")
        for nAirexIdx = 0 to 5
          if statusValves[sensorValve[nAirexIdx]] == 0
            bDoAirex = false
            statusDoAirex = colorError
            call print("Error: no vacuum at position " + toString("1.0", nAirexIdx))
          endIf
        endFor
      else 
        // -- check only those that are selected
        call print("check vacuum on selection")
        for nAirexIdx = 0 to 5
          if statusValves[sensorValve[nAirexIdx]] == 0 and (bSelectedItems[nAirexIdx]==true)
            bDoAirex = false
            statusDoAirex = colorError
            call print("Error: no vacuum at position " + toString("1.0", nAirexIdx))
          endIf
        endFor
      endIf
      if bDoAirex == false
        call print("Fix missing vacuum and try again")
      endIf
      
    endIf
    
    // -- return above does not work. It terminates the task!
    if bDoAirex==true
      
      statusDoAirex = colorRunning
      call setSpeed()
      
      if bSkipGlue[0]==false
        call print("picking glue stamp")
        // -- pick glue stamp
        call moveDown(airexGlueStampHome, mVerySlow, trZero)
        switchMagnet=true
        // FIXME add intermediate step to trZclose?
        movel(appro(airexGlueStampHome, trZsafe), tTool, mVerySlow)

        // -- dip stamp into glue before starting the normal procedure
        call moveDown(glueFirstPoint[0], mVerySlow, trZero)
        delay(waitGlue)
        movej(appro(glueFirstPoint[0], trZclose), tTool, mVerySlow)
        movej(appro(glueFirstPoint[0], trZsafe), tTool, mSlow)
      
        // -- apply glue stamp to all positions:
        //    divide approach onto /from glue position into two steps
        //    (1) slowly to trApproach above it
        //    (2) extremely slowly down to/up from glue to trApproach 
        for nAirexIdx=0 to 5
          if !bSelectItem or (bSelectedItems[nAirexIdx]==true)
            call moveDown(gluePickPoint[nAirexIdx], mVerySlow, trZero)
            
            call print("applying glue to position " + toString("1.0", nAirexIdx))
            delay(waitGlue)
            movej(appro(gluePickPoint[nAirexIdx], trZclose), tTool, mVerySlow)
            movej(appro(gluePickPoint[nAirexIdx], trZsafe), tTool, mSlow)
        
            // -- move to where the glue should be applied (the final airex position)
            call moveDown(airexFinal[nAirexIdx], mExtremelySlow, trStamp)
            delay(waitGlue)
            movej(appro(airexFinal[nAirexIdx], trZclose), tTool, mExtremelySlow)
            movej(appro(airexFinal[nAirexIdx], trZsafe), tTool, mSlow)
          endIf
        endFor

        // -- Drop glue stamp
        call print("put away glue stamp")
        call moveDown(airexGlueStampEnd, mExtremelySlow, trZero)
       
        switchMagnet = false
        movel(appro(airexGlueStampEnd, trZsafe), tTool, mVerySlow)

      endIf
      
      if !bSelectItem
        call print("handling all airex frames")
      else 
        call print("handling selected airex frames")
      endIf
      
      // -- pick weighted vacuum chucks and lift airex frames to HDIs
      for nAirexIdx=0 to 5
        if !bSelectItem or (bSelectedItems[nAirexIdx]==true)
          // -- move over/to chuck
          call moveDown(chuck1[nAirexIdx], mVerySlow, trZero)
          // -- the following cures the click when the chuck 'jumps' up a bit
          movel(appro(chuck1[nAirexIdx], trDownTwo), tTool, mExtremelySlow)
          waitEndMove()
         
          // -- pick chuck and move up
          switchMagnet=true
          delay(waitShort)
          call print("picking airex frame " + toString("1.0", nAirexIdx))
          movel(appro(chuck1[nAirexIdx], trZsafe), tTool, mVerySlow)
          waitEndMove()
          
          // -- move with chuck to airex position
          call moveDown(airexInitial[nAirexIdx], mVerySlow, trZero)
        
          // -- release airex vacuum to allow picking up
          call toggleValve(airexValve[nAirexIdx])
          delay(waitShort)
          switchVac0=true
          switchVac1=true
          delay(waitShort)

          // -- move up with airex frame
          movel(appro(airexInitial[nAirexIdx], trZclose), tTool, mVerySlow)
          // waitEndMove()
          movel(appro(airexInitial[nAirexIdx], trZsafe), tTool, mSlow)
          // likely not required waitEndMove()
          
          // -- move to final airex position and put down 
          call moveDown(airexFinal[nAirexIdx], mExtremelySlow, trZero)
         
          switchVac0   = false
          switchVac1   = false
          switchMagnet = false

          delay(waitShort)

          // -- move up again
          movel(appro(airexFinal[nAirexIdx], trZsafe), tTool, mVerySlow)
          waitEndMove()
        endIf
      endFor

      // -- move back somewhere
      call print("parking robot")
      waitEndMove()
      movej(jInitialPosition, tTool, mNomSpeed)
      waitEndMove()
      statusDoAirex = colorOn
      
      call selectedClear()
      bDoAirex=false
      call print("glue airex done")
    endIf
  endWhile

end]]></Code>
  </Program>
</Programs>