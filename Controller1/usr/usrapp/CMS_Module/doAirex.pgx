<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="doAirex">
    <Locals>
      <Local name="nAirexIdx" type="num" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  // FIXME: I need a local variable to ensure that the vacuum check is run before the real work!
  // Else a race condition exists that the 'real work' could be activated before the check is done. 
  // the delay(1) below is bogus!
  while true
    delay(1)
    if bDoAirex==true
      call selectedCheck()
      call print("starting doAirex")
      
      // -- check that relevant vacuum is active
      if !bSelectItem
        // -- check all
        call print("check vacuum on all")
        delay(waitGlue)
        for nAirexIdx = 0 to 5
          if statusValves[sensorValve[nAirexIdx]] == 0
            bDoAirex = false
            statusDoAirex = colorError
            call print("Error: no vacuum at position " + toString("1.0", nAirexIdx))
          endIf
        endFor
      else 
        // -- check only those that are selected
        call print("check vacuum on selection")
        delay(waitGlue)
        for nAirexIdx = 0 to 5
          if statusValves[sensorValve[nAirexIdx]] == 0 and (bSelectedItems[nAirexIdx]==true)
            bDoAirex = false
            statusDoAirex = colorError
            call print("Error: no vacuum at position " + toString("1.0", nAirexIdx))
          endIf
        endFor
      endIf
      if bDoAirex == false
        call print("Fix missing vacuum and try again")
      endIf
      
    endIf
    
    // -- return above does not work. It terminates the task!
    if bDoAirex==true
      statusDoAirex = colorRunning
      call setSpeed()
      
      if bSkipGlue[0]==false
        call print("picking glue stamp")
        // -- pick glue stamp
        call moveDown(airexGlueStampHome, mSlow, trZero, false)
        switchMagnet=true
        movel(appro(airexGlueStampHome, trZclose), tTool, mVerySlow)
        movel(appro(airexGlueStampHome, trZsafe), tTool, mSlow)
        
        // -- dip stamp into glue before starting the normal procedure
        call moveDown(glueFirstPoint[0], mSlow, trZero, false)
        delay(waitGlue)
        call moveStampUp(glueFirstPoint[0], mSlow)
      
        // -- apply glue stamp to all positions:
        //    divide approach onto /from glue position into two steps
        //    (1) slowly to trApproach above it
        //    (2) extremely slowly down to/up from glue to trApproach 
        for nAirexIdx=0 to 5
          if !bSelectItem or (bSelectedItems[nAirexIdx]==true)
            if bDoAirex==true
              call setSpeed()
              call moveDown(gluePickPoint[nAirexIdx], mSlow, trZero, false)         
              delay(waitGlue)
              
              call moveStampUp(gluePickPoint[nAirexIdx], mVerySlow)
            
              // -- move to where the glue should be applied (the final airex position)
              call print("applying glue to position " + toString("1.0", nAirexIdx))
              call moveDown(airexFinal[nAirexIdx], mSlow, trStamp, false)
              delay(waitGlue)
              call moveStampUp(airexFinal[nAirexIdx], mExtremelySlow)
              
            endIf
          endIf
        endFor

        // -- Drop glue stamp
        call setSpeed()
        call print("put away glue stamp")
        movel(appro(airexGlueStampEnd, trZsafe), tTool, mNomSpeed)
        call moveDown(airexGlueStampEnd, mVerySlow, trZero, true)
        call setSpeed()
        
        switchMagnet = false
        movel(appro(airexGlueStampEnd, trZsafe), tTool, mSlow)

      endIf
      
      if !bSelectItem
        call print("handling all airex frames")
      else 
        call print("handling selected airex frames")
      endIf
      
      // -- move over airex chuck position
      movel(appro(chuck1[0], trZsafe), tTool, mNomSpeed)
      
      // -- pick weighted vacuum chucks and lift airex frames to HDIs
      for nAirexIdx=0 to 5
        if !bSelectItem or (bSelectedItems[nAirexIdx]==true)
          if bDoAirex==true
            call setSpeed()
            // -- move over/to chuck
            call moveDown(chuck1[nAirexIdx], mVerySlow, trZero, false)
            // -- the following cures the click when the chuck 'jumps' up a bit
            movel(appro(chuck1[nAirexIdx], trPressTwo), tTool, mVerySlow)
            waitEndMove()
         
            // -- pick chuck and move up
            switchMagnet=true
            delay(waitShort)
            call setSpeed()
            call print("picking airex frame " + toString("1.0", nAirexIdx))
            movel(appro(chuck1[nAirexIdx], trZsafe), tTool, mSlow)
            waitEndMove()
          
            // -- move with chuck to airex position
            call setSpeed()
            call moveDown(airexInitial[nAirexIdx], mVerySlow, trZero, true)
        
            // -- release airex vacuum to allow picking up
            call setSpeed()
            call toggleValve(airexValve[nAirexIdx])
            delay(waitShort)
            switchVac0=true
            switchVac1=true
            delay(waitShort)

            // -- move up with airex frame
            call print("placing airex frame " + toString("1.0", nAirexIdx))
            call setSpeed()
            movel(appro(airexInitial[nAirexIdx], trZclose), tTool, mVerySlow)
            // waitEndMove()
            movel(appro(airexInitial[nAirexIdx], trZsafe), tTool, mSlow)
            // likely not required waitEndMove()
          
            // -- move to final airex position and put down 
            call moveDown(airexFinal[nAirexIdx], mVerySlow, trZero, true)
         
            switchVac0   = false
            switchVac1   = false
            switchMagnet = false

            delay(waitShort)

            // -- move up again
            call setSpeed()
            movel(appro(airexFinal[nAirexIdx], trZclose), tTool, mVerySlow)
            movel(appro(airexFinal[nAirexIdx], trZsafe), tTool, mSlow)
            waitEndMove()
          endIf
        endIf
      endFor

      // -- move back somewhere
      call print("parking robot")
      waitEndMove()
      movej(jInitialPosition, tTool, mNomSpeed)
      waitEndMove()
      statusDoAirex = colorOn
      
      call selectedClear()
      bDoAirex=false
      call print("glue airex done")
    endIf
  endWhile

end]]></Code>
  </Program>
</Programs>